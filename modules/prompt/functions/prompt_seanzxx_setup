#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sean Zeng <seanzxx@me.com>
#
# Screenshots:
#   http://i.imgur.com/nBEEZ.png
#

# Load dependencies.
pmodload 'helper'

function prompt_seanzxx_get_info {
  if (( $+functions[$1-info] )); then
    unset $1_info
    
    {
      $1-info
      typeset -p $1_info >> "$_prompt_seanzxx_precmd_async_data"
      echo "unset '_prompt_seanzxx_precmd_async_pid[$1]'" >> "$_prompt_seanzxx_precmd_async_data"

      kill -WINCH $$
    } &!

    _prompt_seanzxx_precmd_async_pid[$1]=$!
  fi
}

function prompt_seanzxx_apply_info {
  alias typeset='typeset -g'

  source "$_prompt_seanzxx_precmd_async_data"

  unalias typeset

  # Reset the prompt
  zle && zle reset-prompt
}

function prompt_seanzxx_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS


  # Kill all running jobs
  for pid in $_prompt_seanzxx_precmd_async_pid; do
    kill -KILL $pid 2> /dev/null
  done

  # Reset Variables
  _prompt_seanzxx_precmd_async_pid=()
  echo '' >! "$_prompt_seanzxx_precmd_async_data"

  trap prompt_seanzxx_apply_info WINCH

  # Get informations.
  prompt_seanzxx_get_info 'git'
  prompt_seanzxx_get_info 'python'
  prompt_seanzxx_get_info 'ruby'
  prompt_seanzxx_get_info 'node'
}

function prompt_seanzxx_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Async prompt variables.
  typeset -Ag _prompt_seanzxx_precmd_async_pid
  _prompt_seanzxx_precmd_async_data="${TMPPREFIX}-prompt_seanzxx_data"

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_seanzxx_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '%%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%F{202}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format '%%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format '%%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '%F{202}[%f$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u%F{202}]%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:version' format '%v'
  zstyle ':prezto:module:python:info:virtualenv' format ':%v'
  zstyle ':prezto:module:python:info' format '%F{24}[%e%i]%f'


  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{9}[%v]%f'

  # Set node-info parameters.
  zstyle ':prezto:module:node:info:version' format '%F{76}[%v]%f'

  # Define prompts.
  PROMPT='%B%F{green}%n@%m%f%b %F{white}[%~]%f'
  PROMPT+='${git_info:+ ${(e)git_info[status]}}'
  PROMPT+='${python_info:+ ${python_info[all]}}'
  PROMPT+='${ruby_info:+ $ruby_info[version]}'
  PROMPT+='${node_info:+ $node_info[version]}'
  PROMPT+=$'\n''%F{blue}->%B %#%b%f '

  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_seanzxx_setup "$@"

