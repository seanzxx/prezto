#
# Exposes information about the Python environment via the $python_info
# associative array.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

local version
local version_format
local version_formatted

local virtualenv_format
local virtualenv_formatted

local info_format
local info_formatted

# Clean up previous $python_info.
unset python_info
typeset -gA python_info

if (( $+commands[python] )); then
  version="${$(python --version 2>&1)[(w)2]}"
fi

# Format version.
if [[ -n "$version" ]]; then
  zstyle -s ':prezto:module:python:info:version' format 'version_format'
  zformat -f version_formatted "$version_format" "v:$version"
  python_info[version]="$version_formatted"
fi

# Format virtualenv.
if [[ -n "$VIRTUAL_ENV" ]]; then
  zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
  zformat -f virtualenv_formatted "$virtualenv_format" "v:${VIRTUAL_ENV:t}"
  python_info[virtualenv]="$virtualenv_formatted"
fi

# Format info.
zstyle -s ':prezto:module:python:info' format 'info_format'
zformat -f info_formatted "$info_format" "e:$version_formatted" "i:$virtualenv_formatted" 
python_info[all]="$info_formatted"